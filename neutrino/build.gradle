plugins {
    id 'java-library'
    id 'maven-publish'
    id 'de.fuerstenau.buildconfig' version '1.1.8'
    id 'com.palantir.git-version' version '0.11.0'
    id 'com.jfrog.bintray' version '1.8.4'
    id "com.jfrog.artifactory" version "4.7.5"
}

compileJava {
    sourceCompatibility = '11'
    targetCompatibility = '11'
    options.encoding = 'UTF-8'
}

group = 'de.hhu.bsinfo'
version = getProperty('projectVersion')

compileJava {
    dependsOn(':jni:build', ':neutrino:generateBuildConfig')
    options.compilerArgs += ['-h', project.file('src/main/cpp/include')]
}

repositories {
    mavenCentral()
    jcenter()

    maven {
        url "https://plugins.gradle.org/m2/"
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

javadoc.failOnError = false

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    
    api 'org.slf4j:slf4j-api:1.7.26'
    api 'io.reactivex.rxjava2:rxjava:2.2.9'
    implementation 'org.jetbrains:annotations:17.0.0'
    implementation 'javax.inject:javax.inject:1'
    
    testImplementation 'com.greghaskins:spectrum:1.2.0'
    testImplementation 'org.assertj:assertj-core:3.12.2'
}

buildConfig {
    def gitVersion = versionDetails()
    
    appName = project.name
    version = project.version

    clsName = 'BuildConfig' 
    packageName = 'de.hhu.bsinfo.neutrino.generated'
    
    charset = 'UTF-8'
    
    buildConfigField 'String', 'BUILD_DATE', new Date().format('yyyy-MM-dd HH:mm:ss')
    buildConfigField 'String', 'GIT_COMMIT', gitVersion.gitHash
    buildConfigField 'String', 'GIT_BRANCH', gitVersion.branchName
}

apply from: 'publish.gradle'
apply from: 'bintray.gradle'
apply from: 'artifactory.gradle'